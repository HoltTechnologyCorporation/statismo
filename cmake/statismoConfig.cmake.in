#
# statismoConfig.cmake - Statismo CMake configuration file for external projects.
#
# This file is configured by Statismo and used by the UseStatismo.cmake module
# to load Statismo settings for an external project.
#
# This defines:
# - statismo_LIBRARY_DIR
# - statismo_BINARY_DIR
# - statismo_LIBRARIES
# - statismo_PYTHON_DIR (optionally if statismo was build with wrapping)
#
# This also provides variables about dependencies
# that are used in UseStatismo.cmake or can be used to include
# dependencies manually
#

@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/statismoTargets.cmake")
check_required_components("@PROJECT_NAME@")

# Config file for statismo package

set(_statismo_vtk_support @VTK_SUPPORT@)
set(_statismo_itk_support @ITK_SUPPORT@)
set(_statismo_use_itk_hdf5 @USE_ITK_HDF5@)
set(_statismo_with_wrapping @BUILD_WRAPPING@)

# Compute paths
get_filename_component(statismo_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
set(STATISMO_USE_FILE "${statismo_CMAKE_DIR}/UseStatismo.cmake")

# Our library dependencies (contains definitions for IMPORTED targets)
if(NOT TARGET statismo::statismo_core AND NOT statismo_BINARY_DIR)
  include("${statismo_CMAKE_DIR}/statismoTargets.cmake")
endif()

if (_statismo_itk_support AND _statismo_use_itk_hdf5)
  set(HDF5_INCLUDE_DIRS "@CONFIG_HDF5_INCLUDE_DIRS@")
  set(HDF5_LIBRARIES "@CONFIG_HDF5_LIBRARIES@")
else()
  set(HDF5_DIR "@CONFIG_HDF5_DIR@")
endif()

set(EIGEN3_INCLUDE_DIR "@CONFIG_EIGEN3_INCLUDE_DIR@")

# Including ITK before VTK seems to solve some issues
# with macro redefinition due to the use of itkVtkGkue
if(_statismo_itk_support)
  set(ITK_DIR "@ITK_DIR@")
endif()

if(_statismo_vtk_support)
  set(VTK_DIR "@VTK_DIR@")
endif()

set(statismo_LIBRARY_DIR "@INSTALL_LIB_DIR@")
set(statismo_BINARY_DIR "@INSTALL_BIN_DIR@")
set(statismo_LIBRARIES "@statismo_LIBRARIES@")

if (_statismo_with_wrapping)
  set(statismo_PYTHON_DIR "@INSTALL_LIB_DIR@/python@PYTHON_VERSION_MAJOR@.@PYTHON_VERSION_MINOR@/site-packages")
endif()

